(ns 인스타파스.마크다운
  (:require [instaparse.core :as insta]
            [clojure.test :refer :all]))

(def 분석
  (insta/parser
   "문서         := 문단*
    문단         := 평문? LF / 평문
    <평문>       := (강조 / ANY)+
    <강조>       := 굵게 / 기울임

    굵게         := 별굵게 / 밑줄굵게
    <별굵게>     := <'**'> (!'**' ANY+) <'**'>
    <밑줄굵게>   := <'__'> (!'__' ANY+) <'__'>

    기울임       := 별기울임 / 밑줄기울임
    <별기울임>   := <'*'> (ESC / ANY)+ <'*'>
    <밑줄기울임> := <'_'> (ESC / ANY)+ <'_'>

    ESC    := '\\\\*' | '\\\\_'
    <ANY>  := #'.'
    <LF>   := <'\\n'>
   "))

(deftest 분석테스트
  (testing "문단"
    (are [시도 결과] (= 시도 결과)
      (분석 "")
      [:문서]

      (분석 "문장 하나\n문장 둘.\n\n")
      [:문서
       [:문단 "문" "장" " " "하" "나"]
       [:문단 "문" "장" " " "둘" "."]
       [:문단]]))

  (testing "강조"
    (are [시도 결과] (= 시도 결과)
      (분석 "*이탤릭* 시작")
      [:문서 [:문단 [:기울임 "이" "탤" "릭"] " " "시" "작"]]

      (분석 "_이탤릭_으로 시작")
      [:문서 [:문단 [:기울임 "이" "탤" "릭"] "으" "로" " " "시" "작"]]

      (분석 "_이탤릭\\_이스케이프_")
      [:문서 [:문단 [:기울임 "이" "탤" "릭" [:ESC "\\_"] "이" "스" "케" "이" "프"]]]

      (분석 "**굵게*강조**한 문장")
      [:문서 [:문단 [:굵게 "굵" "게" "*" "강" "조"] "한" " " "문" "장"]]

      (분석 "이렇게 __굵게_강조__한 문장")
      [:문서 [:문단 "이" "렇" "게" " " [:굵게 "굵" "게" "_" "강" "조"] "한" " " "문" "장"]]

      )))
