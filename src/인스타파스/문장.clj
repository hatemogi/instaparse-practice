(ns 인스타파스.문장
  (:require [instaparse.core :as insta]
            [clojure.test :refer :all]))

(def 파서
  (insta/parser
   "문장       := (링크 / 각주 / 강조 / ANY)+

    (* 링크 *)
    <링크>     := 일반링크 / 자동링크 / 참조링크
    일반링크   := <'['> 텍스트 <']('> 주소 <')'>
    텍스트     := (강조 / ANY)+
    주소       := eTXT

    자동링크   := <'<'> ANY+ '://' (!'>' ANY)+ <'>'>

    참조링크   := <'['> 텍스트 <']' 공백? '['> 참조이름 <']'> / <'['> 참조이름 <'][]'>
    참조이름   := (!']' ANY)+

    (* 각주 *)
    각주       := 측주 / 미주
    측주       := <'[>'> 참조이름 <']'>
    미주       := <'[^'> 참조이름 <']'>

    (* 강조 *)
    <강조>     := 굵게 / 기울임 / 코드
    굵게       := <'**'> ANY+ <'**'> / <'__'> ANY+ <'__'>
    기울임     := <'*'> ANY+ <'*'> / <'_'> ANY+ <'_'>
    코드       := <'```'> (!'```' ANY ANY? ANY?)+ <'```'> /
                  <'``'> (!'``' ANY ANY?)+ <'``'> /
                  <'`'> eTXT <'`'>

    <eTXT>     := (ESC / ANY)+
    ESC        := #'\\\\[\\`*_{}\\[\\]()#+\\-.!]'
    <ANY>      := #'.'
    공백       := #'\\s'
    <LF>       := '\\n'
    <숫자>     := #'[0-9]'
   "))

(def 분석 파서)

(deftest 분석테스트

  (testing "각주"
    (are [시도 결과] (= 시도 결과)
      (분석 "미주를 달아보자.[^1]")
      [:문장 "미" "주" "를" " " "달" "아" "보" "자" "." [:각주 [:미주 [:참조이름 "1"]]]]

      (분석 "측주를 달아보자.[>1]")
      [:문장 "측" "주" "를" " " "달" "아" "보" "자" "." [:각주 [:측주 [:참조이름 "1"]]]]))

  (testing "링크"
    (are [시도 결과] (= 시도 결과)
      (분석 "[링크](주소)를 걸어보자")
      [:문장 [:일반링크 [:텍스트 "링" "크"] [:주소 "주" "소"]] "를" " " "걸" "어" "보" "자"]

      (분석 "[*강조*링크](주소)걸기")
      [:문장 [:일반링크 [:텍스트 [:기울임 "강" "조"] "링" "크"] [:주소 "주" "소"]] "걸" "기"]

      (분석 "[[링크]](주소)걸기")
      [:문장 [:일반링크 [:텍스트 "[" "링" "크" "]"] [:주소 "주" "소"]] "걸" "기"]

      (분석 "<http://자동.com/> 테스트>")
      [:문장 [:자동링크 "h" "t" "t" "p" "://" "자" "동" "." "c" "o" "m" "/"] " " "테" "스" "트" ">"]

      (분석 "[참조 링크][1]")
      [:문장 [:참조링크 [:텍스트 "참" "조" " " "링" "크"] [:참조이름 "1"]]]

      (분석 "[참조 링크][]")
      [:문장 [:참조링크 [:참조이름 "참" "조" " " "링" "크"]]]

      (분석 "[참조 링크] [1]")
      (분석 "[참조 링크][1]")))

  (testing "강조"
    (are [시도 결과] (= 시도 결과)
      (분석 "*이탤릭* 시작")
      [:문장 [:기울임 "이" "탤" "릭"] " " "시" "작"]

      (분석 "_이탤릭_으로 시작")
      [:문장 [:기울임 "이" "탤" "릭"] "으" "로" " " "시" "작"]

      (분석 "**굵게*강조** *이탤릭*한 문장")
      [:문장 [:굵게 "굵" "게" "*" "강" "조"] " " [:기울임 "이" "탤" "릭"] "한" " " "문" "장"]

      (분석 "이렇게 __굵게_강조__한 문장")
      [:문장 "이" "렇" "게" " " [:굵게 "굵" "게" "_" "강" "조"] "한" " " "문" "장"]

      (분석 "이렇게 `코드` 인용한 문장")
      [:문장 "이" "렇" "게" " " [:코드 "코" "드"] " " "인" "용" "한" " " "문" "장"]

      (분석 "이렇게 ``a`b`` 인용한 문장")
      [:문장 "이" "렇" "게" " " [:코드 "a" "`" "b"] " " "인" "용" "한" " " "문" "장"])))
